# Author: Lucas Vilas-Boas
# Year: 2024
# Repo: https://github.com/lucoiso/vk-imgui-template-project

# ----------- Global Definitions -----------
SET(LIBRARY_NAME UserInterface)

# ------------- Library Setup --------------
SET(PRIVATE_MODULES_BASE_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/Source/Private)
SET(PUBLIC_MODULES_BASE_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/Source/Public)
SET(RESOURCE_BASE_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/Resources)

SET(PRIVATE_MODULES
        "${PRIVATE_MODULES_BASE_DIRECTORY}/AppLibrary.cxx"
        "${PRIVATE_MODULES_BASE_DIRECTORY}/AppWindow.cxx"
        "${PRIVATE_MODULES_BASE_DIRECTORY}/AppMainView.cxx"
        "${PRIVATE_MODULES_BASE_DIRECTORY}/AppWin32ParentingHelper.cxx"
)

SET(PUBLIC_MODULES
        "${PUBLIC_MODULES_BASE_DIRECTORY}/AppLibrary.ixx"
        "${PUBLIC_MODULES_BASE_DIRECTORY}/AppWindow.ixx"
        "${PUBLIC_MODULES_BASE_DIRECTORY}/AppMainView.ixx"
        "${PUBLIC_MODULES_BASE_DIRECTORY}/AppWin32ParentingHelper.ixx"
)

SET(PUBLIC_HEADERS
        "${PUBLIC_MODULES_BASE_DIRECTORY}/UserInterfaceModule.hpp"
)

ADD_LIBRARY(${LIBRARY_NAME} SHARED ${PRIVATE_MODULES})
SET_TARGET_PROPERTIES(${LIBRARY_NAME} PROPERTIES LINKER_LANGUAGE CXX)

TARGET_SOURCES(${LIBRARY_NAME}
        PUBLIC
        FILE_SET cxx_public_modules
        TYPE CXX_MODULES
        BASE_DIRS ${PUBLIC_MODULES_BASE_DIRECTORY}
        FILES ${PUBLIC_MODULES}
)

TARGET_SOURCES(${LIBRARY_NAME}
        PUBLIC
        FILE_SET cxx_public_headers
        TYPE HEADERS
        BASE_DIRS ${PUBLIC_MODULES_BASE_DIRECTORY}
        FILES ${PUBLIC_HEADERS}
)

TARGET_INCLUDE_DIRECTORIES(${LIBRARY_NAME} PRIVATE ${PRIVATE_MODULES_BASE_DIRECTORY})
TARGET_INCLUDE_DIRECTORIES(${LIBRARY_NAME} PUBLIC ${PUBLIC_MODULES_BASE_DIRECTORY})
TARGET_COMPILE_DEFINITIONS(${LIBRARY_NAME} PRIVATE BUILD_DLL)

FIND_PACKAGE(Boost REQUIRED COMPONENTS log)
TARGET_LINK_LIBRARIES(${LIBRARY_NAME} PUBLIC
        luGUI
        Boost::log
)

INCLUDE(GNUInstallDirs)
INSTALL(TARGETS ${LIBRARY_NAME}
        BUNDLE DESTINATION .
        LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
        RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
)

ADD_CUSTOM_TARGET(APPLICATION_COPY_RESOURCES
        COMMAND ${CMAKE_COMMAND} -E copy_directory_if_different "${RESOURCE_BASE_DIRECTORY}" "${CMAKE_INSTALL_BINDIR}/Resources"
        COMMENT "[CMake Command] [Application]: Copying application resources"
)